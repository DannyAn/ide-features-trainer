plugins {
    id 'org.jetbrains.intellij' version '0.4.5'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
    id 'de.undercouch.download' version '3.4.3'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

group 'org.sk'
version "$version.$buildNumber"

sourceCompatibility = 1.8
targetCompatibility = 1.8

intellij {
    version 'IU-191-SNAPSHOT'

    plugins = [
            'Pythonid:2019.1.191.6183.20',
            'org.jetbrains.plugins.ruby:2019.1.20190314',
            'com.intellij.testGuiFramework:0.9.37@nightly'
    ]
    if (System.getProperty("idea.gui.test.alternativeIdePath") != null) {
        alternativeIdePath System.getProperty("idea.gui.test.alternativeIdePath")
    }
    updateSinceUntilBuild false
}

sourceSets {
    main {
        java.srcDir 'src'
        kotlin.srcDir 'src'
        resources.srcDir 'res'
    }
    test {
        java.srcDir 'testSrc'
        java.srcDir 'testsData'
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task testsJar(type: Jar, dependsOn: classes) {
    classifier = 'tests'
    from sourceSets.test.output
    exclude 'testData/*'
}

prepareSandbox {
    from(testsJar) {
        into "training/lib"
    }
}

prepareTestingSandbox {
    from(testsJar) {
        into "training/lib"
    }
}

runIde {
    if (System.getProperty("idea.gui.tests.gradle.runner").equals("true")) {
        systemProperties System.properties.findAll {
            (it.key as String).startsWith("idea") || (it.key as String).startsWith("jb")
        }
        /* Need to split the space-delimited value in the exec.args */
        print systemProperties
        args System.getProperty("exec.args", "").split(",")
    }
}

test{
    def sysProps = System.properties.findAll { (it.key as String).startsWith("idea") || (it.key as String).startsWith("jb") }
    sysProps.put("idea.gui.tests.gradle.runner", true) //Use Gradle Launcher to run GUI tests
    systemProperties sysProps
}